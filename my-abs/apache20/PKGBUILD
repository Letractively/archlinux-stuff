# $Id: PKGBUILD,v 1.3 2006/07/18 09:45:51 pupykin Exp $
# Maintainer: judd <jvinet@zeroflux.org>
pkgname=apache20
pkgver=2.0.58
pkgrel=4.2
pkgdesc="A high performance Unix-based HTTP server (with SSL)"
backup=(etc/httpd/conf/httpd.conf home/httpd/html/index.html \
        etc/conf.d/httpd etc/httpd/conf/ssl.conf)
depends=('glibc' 'gdbm' 'db' 'openssl' 'expat')
provides=('apr' 'apr-util' 'apache')
conflicts=('apr' 'apr-util' 'apache')
options=("NOLIBTOOL")
source=(http://apache.rinet.ru/dist/httpd/httpd-$pkgver.tar.gz \
        httpd index.html mod_ssl.txt httpd.logrotate httpd.conf.d \
        httpd.conf ssl.conf)
md5sums=('a03eeefee78c01ec24c8671380763860' '077b2d9dcb7914600f5b1a3de1dec8f7'\
        '64206bf5da4daf4b962186ded0a3d20a' '2e68c9508d21e98c4503dcb159d22890'\
	'a13925eef67108cf22f6bd1b2b8decb1' '62dba8077401cb5515ecb79fe29c2a52'\
	'8747daec5196bdb6221f7dc334504f90' '7adfc868a67f9b16d63a9f3b074f3efa')
                                       
build() {                              
  cd $startdir/src/httpd-$pkgver
  # fix the suexec user
  sed -i 's|^#define AP_HTTPD_USER.*$|#define AP_HTTPD_USER "nobody"|' \
    support/suexec.h
  ./configure --prefix=/usr --enable-layout=RedHat --datadir=/home/httpd \
    --enable-modules=all --enable-mods-shared=all --enable-ssl --enable-so \
    --enable-proxy --enable-proxy-connect --enable-proxy-ftp \
    --enable-proxy-http --enable-suexec
  make || return 1                     
  make DESTDIR=$startdir/pkg install   
  rm -f $startdir/pkg/usr/include/fnmatch.h
  rm -rf $startdir/pkg/home/httpd/html/* $startdir/pkg/home/httpd/manual
  install -D -m755 ../httpd $startdir/pkg/etc/rc.d/httpd
  install -D -m644 ../httpd.logrotate $startdir/pkg/etc/logrotate.d/httpd
  install -D -m644 ../index.html $startdir/pkg/home/httpd/html/index.html
  install -D -m644 ../mod_ssl.txt $startdir/pkg/etc/httpd/conf/mod_ssl.txt
  install -D -m644 ../httpd.conf.d $startdir/pkg/etc/conf.d/httpd
  install -D -m644 ../httpd.conf $startdir/pkg/etc/httpd/conf/httpd.conf
  install -D -m644 ../ssl.conf $startdir/pkg/etc/httpd/conf/ssl.conf
  # move suexec to the right place
  mv $startdir/pkg/usr/sbin/suexec $startdir/pkg/usr/bin/suexec

#  find $startdir/pkg -name '*.la' -exec rm {} \;
}
