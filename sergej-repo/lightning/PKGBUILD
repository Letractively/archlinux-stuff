# Maintainer: TDY <tdy@gmx.com>

pkgname=lightning
pkgver=0.9
pkgrel=3
pkgdesc="An integrated calendar extension for Mozilla Thunderbird"
arch=('i686' 'x86_64')
url="http://www.mozilla.org/projects/calendar/lightning/"
license=('MPL' 'GPL' 'LGPL')
depends=('thunderbird')
makedepends=('pkgconfig' 'zip')
options=('!makeflags')
source=(http://releases.mozilla.org/pub/mozilla.org/calendar/sunbird/releases/$pkgver/source/$pkgname-sunbird-$pkgver-source.tar.bz2
        $pkgname-sunbird-$pkgver-locale.diff $pkgname-gcc44.diff mozconfig)
md5sums=('7757ffefd4a30bcc1497b93b3dc6c0ce'
         'e6916365e044e6b168d131f11f9bf7a5'
         'a1d328cf1aa967dc1cfcacbfada9a8b7'
         'dfcc2b4ea1e119a730065800e2b47a14')

build() {
  cd "$srcdir/mozilla"

  export MOZ_OBJDIR="$srcdir/mozilla/$pkgname-obj"
  export MOZILLA_FIVE_HOME=/usr/lib/thunderbird-2.0
  export MOZCONFIG="$srcdir/mozconfig"
  export LDFLAGS="$(pkg-config --libs xrender)"

  patch -Np1 -i ../$pkgname-sunbird-$pkgver-locale.diff || return 1
  patch -Np1 -i ../$pkgname-gcc44.diff || return 1

  (make -f client.mk build      && \
   make -C "$MOZ_OBJDIR" export && \
   make -C "$MOZ_OBJDIR/xpcom"  && \
   make -C "$MOZ_OBJDIR/js"     && \
   make -C "$MOZ_OBJDIR/calendar/$pkgname") || return 1

  MOZILLA_FIVE_HOME+=/extensions/$(sed -n '70 s/.*\({.*}\).*/\1/p' \
    calendar/$pkgname/Makefile.in)
  install -dm755 "$pkgdir$MOZILLA_FIVE_HOME"
  bsdtar -xf "$MOZ_OBJDIR/dist/xpi-stage/$pkgname.xpi" \
    -C "$pkgdir$MOZILLA_FIVE_HOME"

  find "$pkgdir" -type d -exec chmod 755 -- '{}' \;
  find "$pkgdir" -type f -exec chmod 644 -- '{}' \;
  find "$pkgdir" -name '*.so*' -o -name '*.sqlite' -exec chmod 755 -- '{}' \;
}
